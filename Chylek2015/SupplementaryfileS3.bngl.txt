begin model

# A model for receptor tyrosine kinase (RTK) signaling
# The receptor is phosphorylated at two Y sites in a basal steady state 
#   due to a low level of receptor dimerization and dimer-dependent
#   autophosphorylation in the absence of ligand and also due to non-specific
#   kinase activity. Background phosphatase activity is high. An adaptor
#   associates with the pY sites in the receptor. 
#   Conservation of mass is assumed.
#   Two compartments are implicit: the plasma membrane, where the receptor is
#   found, and the cytosol. The adaptor, phosphatase, and a phosphatase inhibitor
#   are considered to be able to freely diffuse in the cytosol.

# An equilibration simulation (started at an arbitrary initial condition) 
#   is performed to find the basal steady state in the absence of phosphatase
#   inhibition.
# The effect of adding a phosphatase inhibitor is then simulated.

begin parameters

# Avogadro constant
NA 6.02214e23 # [=] molecules per mol

# cytoplasmic volume
V 1.0e-12 # [=] L (1 pL)

# copy numbers of receptor, adaptor, phosphatase, and phosphatase inhibitor
Rtot 1.0e4 # [=] number per cell
Atot 1.0e5 # [=] number per cell
Ptot 1.0e5 # [=] number per cell
inhtot 1.0e6 # [=] number per cell

# equilibrium association constant for receptor dimerization
#   multiplied by receptor level (expressed in consistent units)
KdimRtot 0.02 # [=] dimensionless

# reverse rate constant for receptor dimerization
kr_dim 1.0 # [=] /s

# forward rate constant for receptor dimerization (derived)
kf_dim=kr_dim*KdimRtot/Rtot # [=] /(molecule/cell)/s

# pseudo first-order rate constants for phosphorylation 
#   of receptor Y sites 1 and 2
#   Autophosphorylation is assumed.
p1_auto 1.0 # [=] /s
p2_auto 0.5 # [=] /s

# pseudo first-order rate constants for phosphorylation
#   of receptor Y sites 1 and 2
#   It is assumed that phosphorylation is mediated by non-specific kinases.
p1_nonsp 0.05 # [=] /s
p2_nonsp 0.05 # [=] /s

# forward and reverse rate constants for adaptor binding 
#   to receptor pY site 1
kf_a1 5.0e5/(NA*V) # [=] /(molecule/cell)/s (1e6 /M/s)
kr_a1 0.05 # [=] /s

# forward and reverse rate constants for adaptor binding 
#   to receptor pY site 2
kf_a2 2.0e6/(NA*V) # [=] /(molecule/cell)/s (1e6 /M/s)
kr_a2 0.2 # [=] /s

# parameters for dephosphorylation of receptor pY site 1
#   by phosphatase via a Michaelis-Menten mechanism
kp1 1.0e6/(NA*V) # [=] /(molecule/cell)/s (1e6 /M/s)
km1 0.1 # /s
kcat1 2.0 # /s

# parameters for dephosphorylation of receptor pY site 2
#   by phosphatase via a Michaelis-Menten mechanism
kp2 1.0e6/(NA*V) # [=] /(molecule/cell)/s (1e6 /M/s)
km2 0.1 # [=] /s
kcat2 0.5 # [=] /s

# forward and reverse rate constants for inhibitor binding to phosphatase
kf_inh 1.0e7/(NA*V) # [=] /(molecule/cell)/s (1e7 /M/s)
kr_inh 0.01 # [=] /s

end parameters

begin molecule types

# receptor
#   The receptor contains a binding site that mediates dimerization
#   and two sites of autophosphorylation.
R(dim,Y1~0~P,Y2~0~P)

# adaptor
#   The adaptor contains an SH2 domain, 
#     which recognizes receptor phosphotyrosines.
A(SH2)

# phosphatase
#   The cat site representes the catalytic domain of the phosphatase.
P(cat)

# phosphatase inhibitor
#   The inhibitor's p site binds the cat site in P.
inh(p)

end molecule types

begin seed species

# All receptors are initially monomeric and unphosphorylated.
R(dim,Y1~0,Y2~0) Rtot

# All adaptors are initially free.
A(SH2) Atot

# All phosphatases are initially free.
P(cat) Ptot

# The amount of free inhibitor present initially is determined by the
#   parameter inhtot, which is adjusted in the actions block.
inh(p) inhtot

end seed species

begin observables

# Number of receptors phosphorylated at site 1
Molecules pY1 R(Y1~P)

# Number of receptors phosphorylated at site 2
Molecules pY2 R(Y2~P)

end observables

begin reaction rules

# spontaneous receptor dimerization
R(dim)+R(dim)<->R(dim!1).R(dim!1) kf_dim,kr_dim

# dimer-dependent receptor autophosphorylation
R(dim!+,Y1~0)->R(dim!+,Y1~P) p1_auto
R(dim!+,Y2~0)->R(dim!+,Y2~P) p2_auto

# non-specific dimer-independent receptor phosphorylation
R(Y1~0)->R(Y1~P) p1_nonsp
R(Y2~0)->R(Y2~P) p2_nonsp

# reversible, phosphorylation-dependent recruitment of adaptor 
#   to phosphorylated receptor sites
A(SH2)+R(Y1~P)<->A(SH2!1).R(Y1~P!1) kf_a1,kr_a1
A(SH2)+R(Y2~P)<->A(SH2!1).R(Y2~P!1) kf_a2,kr_a2

# constitutive phosphatase activity affecting site 1 in the receptor
#   The phosphatase is assumed to act as a Michaelis-Menten enzyme.
P(cat)+R(Y1~P)<->P(cat!1).R(Y1~P!1) kp1,km1
P(cat!1).R(Y1~P!1)->P(cat)+R(Y1~0) kcat1

# constitutive phosphatase activity affecting site 1 in the receptor
#   The phosphatase is assumed to act as a Michaelis-Menten enzyme.
P(cat)+R(Y2~P)<->P(cat!1).R(Y2~P!1) kp2,km2
P(cat!1).R(Y2~P!1)->P(cat)+R(Y2~0) kcat2

# inhibition of phosphatase activity
#   The inhibitor reversibly binds the phosphatase's catalytic domain
inh(p)+P(cat)<->inh(p!1).P(cat!1) kf_inh,kr_inh

end reaction rules

end model

begin actions

generate_network({overwrite=>1})

# equilibrate (to find basal steady state)
setConcentration("inh(p)",0.0)
simulate({method=>"ode",t_start=>-100,t_end=>0,n_steps=>100})

# simulate effect of phosphatase inhibition
setConcentration("inh(p)","inhtot")
simulate({method=>"ode",t_start=>0,t_end=>100,n_steps=>100,continue=>1})

end actions
